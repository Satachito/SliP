//	SliP ver 0.2(Swift) 2016 written by Satoru Ogura, Tokyo.
//	This is sample script for SliP, please push 'CALCULATE' button.

//	Four arithmetic operations
//	Please insert spaces between operator and number.

1 + 2;
1 + 2 × 3;
( 1 + 2 ) × 3;
5 - 3;
5 ÷ 3;
5 / 3;
5 % 3;


//	Bitwise operations

2 | 3;
2 & 3;
2 ^ 3;


//	Memory

'a = 1 + 2;
'b = 3 + 4;
a + b;


//	Logical
//	"T" for true, Nil i.e. '[ ]' for false.

3 == 3;
3 == 4;


//	String

"ABC" + "DEF";


//	If

3 == 3 ? [ "TRUE" "FALSE" ];
3 == 4 ? [ "TRUE" "FALSE" ];


//	Function

'add1 = '( @ + 1 );
3 : add1;
//	@ is function argument. '3 : add1' means apply 3 to function 'add1'.

'factorial = '(
	@ × ( @ == 1 ? [ 1 ( ( @ - 1 ):factorial ) ] )
);
4 : factorial;

'sigma = '(
	@ + ( @ == 0 ? [ 0 ( ( @ - 1 ):sigma ) ] )
);
4 : sigma;

'fibonacci = '(
	@ < 2 ? [
		@
		( ( @ - 1 ):fibonacci + ( @ - 2 ):fibonacci )
	]
);
10:fibonacci;


//	List

[ "A" "B" "C" ] + [ "D" "E" "F" ];
[ "A" "B" "C" ]:0;
[ "A" "B" "C" ]:1;
[ "A" "B" "C" ]:$;
[ "A" "B" "C" ]:#;
[ "A" "B" "C" ]:*;


//	Procedure
{	( 'a = 1 + 2 )
	( 'b = 3 + 4 )
	( a + b )
};

//	Function 2

'gcd = '(
	@:1 == 0 ? [
		( @:0 )
		( { ( @:1 ) ( @:0 % @:1 ) }:gcd )
	]
);
[ 1071 1029 ]:gcd;

'reduce = '(
	@:0:# == 1 ? [
		(@:0:0)
		( { (@:0:0) ( { (@:0:*) (@:1) }:reduce ) }:(@:1) )
	]
);
[ [ 1 2 3 4 ]( @:0 + @:1 ) ]:reduce;
[ [ 1 2 3 4 ]( @:0 × @:1 ) ]:reduce;


